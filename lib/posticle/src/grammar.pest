document = _{
    (non_text | text)*
}

text = {
    (!non_text ~ ANY)+
}

non_text = _{
    emoticon | hashtag | mention | link | line_break
}

line_break = {
    "\r\n" | "\n"
}

emoticon = {
    ":"
    ~ emoticon_name
    ~ ":"
}

emoticon_name = {
    (LETTER | NUMBER | DASH_PUNCTUATION)+
}

hashtag = {
    symbol_prefix
    ~ "#" ~ hashtag_name
    ~ symbol_suffix
}

hashtag_name = {
    (!(symbol_identifiers | symbol_suffix) ~ ANY){1,32}
}

mention = {
    symbol_prefix
    ~ "@" ~ (
          (mention_username ~ !"@")
        | (mention_username ~ "@" ~ mention_domain)
    )
    ~ symbol_suffix
}

mention_username = {
    (!(symbol_identifiers | symbol_suffix) ~ ANY){1,32}
}

mention_domain = {
    (!(symbol_identifiers | symbol_suffix) ~ ANY){1,60}
}

symbol_prefix = {
    line_break | symbol_prefix_char | SOI
}

symbol_prefix_char = {
    WHITE_SPACE | (!symbol_identifiers ~ PUNCTUATION)
}

symbol_suffix = _{
    &((!symbol_identifiers ~ PUNCTUATION)+ ~ (WHITE_SPACE | EOI) | WHITE_SPACE | EOI)
}

symbol_identifiers = _{
    "@" | "#"
}

link = {
    symbol_prefix ~ link_schema ~ link_tail
}

link_schema = {
    ALPHABETIC+ ~ ":" ~ "//"?
}

link_tail = {
    (
        !link_terminator ~ ANY
        | link_tail_dirpath
        | link_tail_youtube
        | link_tail_wikipedia
    )+
}

link_terminator = _{
    link_terminator_punctuation
    | link_terminator_whitespace
    | link_terminator_dirpath
    | link_terminator_youtube
    | link_terminator_wikipedia
}

link_terminator_punctuation = _{
    PUNCTUATION ~ (link_terminator_whitespace)
}

link_terminator_whitespace = _{
    WHITE_SPACE | EOI
}

// https://localhost/path/to/dir/
link_tail_dirpath = _{
    "/"
}

link_terminator_dirpath = _{
    link_tail_dirpath
}

// https://www.youtube.com/watch?v=g8X0nJHrJ9-
link_tail_youtube = _{
    "-"
}

link_terminator_youtube = _{
    link_tail_youtube
}

// https://en.wikipedia.org/wiki/Diaspora_(software)
link_tail_wikipedia = _{
    "("
    ~ (link_tail_wikipedia | !("(" | ")") ~ ANY)*
    ~ ")"
}

link_terminator_wikipedia = _{
    "(" | ")"
}